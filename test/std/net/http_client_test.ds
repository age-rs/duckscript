
fn test_get
    response = http_client https://www.rust-lang.org/

    found = contains ${response} Rust

    assert ${found}
end

fn test_get_to_file
    file = set ./target/_duckscript_test/http_client/page.html
    rm ${file}

    response_size = http_client --output-file ${file} https://www.rust-lang.org/

    response = readfile ${file}
    found = contains ${response} Rust

    assert ${found}
    assert ${response_size}
end

fn test_post
    os = os_family
    is_linux = equals ${os} linux
    if ${is_linux}
        payload = set {\"login\":\"login\",\"password\":\"password\"}
        response = http_client --method POST --payload ${payload} https://postman-echo.com/post

        found = contains ${response} password

        assert ${found}
    end
end

fn test_invalid_url
    error = get_last_error
    empty = is_empty ${error}
    assert ${empty}

    response = http_client invalid_url

    error = get_last_error
    empty = is_empty ${error}
    assert_false ${empty}

    assert_false ${response}
end
